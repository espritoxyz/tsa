#include "../../imports/stdlib.fc";
#include "../../imports/tsa_functions.fc";

global slice initial_sender;
global slice contract_address;
global int initial_value;

() on_internal_message_send(int balance, int msg_value, cell in_msg_full, slice msg_body, int input_id) impure method_id {
    slice cs = in_msg_full.begin_parse();
    cs~load_uint(4); ;; flags
    initial_sender = cs~load_msg_addr(); ;; src
    contract_address = cs~load_msg_addr(); ;; dst
    initial_value = cs~load_coins(); ;; value

    cell c4 = tsa_get_c4(1);
    tsa_make_address_random_and_independent_from(initial_sender, c4.begin_parse());
    tsa_make_address_random_and_independent_from(initial_sender, contract_address);
}

() on_out_message(cell msg_full, slice msg_body, int receiver_contract_id, int sender_contract_id) impure method_id {
    slice cs = msg_full.begin_parse();
    cs~skip_bits(4); ;; message headers
    cs~load_msg_addr(); ;; src address
    slice dest = cs~load_msg_addr();
    int value = cs~load_grams();

    if (equal_slice_bits(dest, initial_sender)) {
        tsa_allow_failures();
        throw_if(1000, value > initial_value);
        tsa_forbid_failures();
    }
}

() main() impure {

    tsa_forbid_failures();
    tsa_send_internal_message(1, 0);
    tsa_allow_failures();

    throw(500);
}
